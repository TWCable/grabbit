plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply from: "${rootProject.projectDir}/gradle/verifyComponentConfig.gradle"
apply from: "${rootProject.projectDir}/gradle/utils.gradle"

apply plugin: 'osgi'
apply plugin: 'scr'
apply plugin: 'sling-bundle'
apply plugin: 'protobuf'

// remove "mvn install" since it's not needed and causes a conflict
def installTask = tasks.findByPath('install')
if (installTask != null) tasks.remove(installTask)

apply plugin: 'cqpackage'

ext.packageName = project.name
ext.bundleName = project.name
ext.bundleDescription = 'Grabbit'
ext.symbolicName = "com.twcable.grabbit"
jar.baseName = 'grabbit'

createPackage.bundleInstallRoot = '/apps/grabbit/install'

bundle.installPath = '/apps/grabbit/install'

jar {
    from('src/main/content/SLING-INF/content', {
        into 'SLING-INF/content'
    })
}

verifyBundles.dependsOn 'jar'

jar.manifest {

    attributes 'Sling-Initial-Content': project.configFolders.collect {
        "SLING-INF/content/apps/grabbit/${it}; overwrite:=true; uninstall:=true; path:=/apps/grabbit/${it}"
    }.join(', ')

    attributes 'Bundle-Name': project.bundleName
    attributes 'Bundle-SymbolicName': project.symbolicName
    attributes 'Bundle-Description': project.bundleDescription
    instruction 'Import-Package', 'groovy.json; version="[2.3,3.0)"'
    instruction 'Import-Package', 'groovy.json.internal; version="[2.3,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.scope; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.item.file.transform; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.item.file; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.item.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.configuration.xml; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.configuration.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.explore.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.repository.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.repository; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.launch.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.job.flow.support; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.job.flow.support.state; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.core.repository.dao; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.batch.support.transaction; version="[2.2,3.0)"'
    instruction 'Import-Package', 'org.springframework.beans.factory.config; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.core.io; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.core.task; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.aop; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.aop.framework; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.aop.scope; version="[3.1,4.0)"'
    instruction 'Import-Package', 'org.springframework.scheduling.concurrent; version="[3.1,4.0)"'
    instruction 'Import-Package', "org.aopalliance.aop;version=1.0.0"
    instruction 'Import-Package', "sun.misc"
    instruction 'Import-Package', '*'

    // export everything to OSGi except *.impl packages
    instruction 'Export-Package', "!*.impl, *;-noimport:=false;version=${version}"
}

// exclude bundles provided by CQ
configurations.cq_package {
    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'commons-httpclient', module: 'commons-httpclient'
    exclude group: 'com.sun.xml.bind'
    exclude group: 'aopalliance', module: 'aopalliance'
    exclude group: 'javax.xml.stream'
    exclude group: 'javax.jcr', module: 'jcr'

    // use AEM's logging, not logback
    exclude group: 'ch.qos.logback', module: 'logback-classic'
    exclude group: 'ch.qos.logback', module: 'logback-core'

    // Slf4j is provided by AEM
    exclude group: 'org.slf4j', module: 'slf4j-api'
    exclude group: 'org.slf4j', module: 'jcl-over-slf4j'

    // excluding Joda Time too as AEM provides its own in com.day.commons.osgi.wrapper.joda-time
    exclude group: 'joda-time', module: 'joda-time'

    exclude group: 'com.google.code.findbugs', module: 'jsr305'

    exclude group: 'com.day.cq.workflow', module: 'cq-workflow-console'

    exclude group: 'org.apache.felix', module: 'org.apache.felix.scr.annotations'
    exclude group: 'org.apache.felix', module: 'org.osgi.core'
    exclude group: 'org.apache.felix', module: 'org.osgi.compendium'

    exclude group: 'org.apache.commons', module: 'commons-lang3'
    exclude group: 'org.apache.jackrabbit', module:'jackrabbit-jcr-commons'
    exclude group: 'commons-io', module: 'commons-io'

    //Exclude Apache Sling Libraries
    exclude group: 'org.apache.sling', module: 'org.apache.sling.api'
    exclude group: 'org.apache.sling', module:'org.apache.sling.jcr.resource'
    exclude group: 'org.apache.sling', module: 'org.apache.sling.jcr.api'

    // 6.x exclude bundles
    exclude group: 'com.google.guava', module: 'guava'
    exclude group: 'org.apache.httpcomponents', module: 'httpclient-osgi'
    exclude group: 'org.apache.httpcomponents', module: 'httpcore-osgi'
    exclude module: 'cq-workflow-console'
}

bundle.installPath = '/apps/grabbit/install/${nativeStartLevel}'

idea {
    module {
        // The whole build dir is excluded by default, but we need build/generated-sources,
        // which contains the generated proto classes.
        excludeDirs = [
                file("$buildDir/classes"),
                file("$buildDir/docs"),
                file("$buildDir/dependency-cache"),
                file("$buildDir/libs"),
                file("$buildDir/reports"),
                file("$buildDir/resources"),
                file("$buildDir/test-results"),
                file("$buildDir/tmp"),
        ]
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(bintrayUpload)) {
        if (!project.hasProperty('bintray.user') || !project.hasProperty('bintray.key')) {
            throw new IllegalArgumentException((String)"Please define 'bintray.user' and " +
                    "'bintray.key' properties. (Such as in ~/.gradle/gradle.properties)")
        }
    }
}

version = new Version(version as String)

bintray {
    user = project.properties['bintray.user']
    key = project.properties['bintray.key']
    filesSpec {
        from tasks.getByPath('createPackage').archivePath
        into '.'
    }

    publish = version.status == 'release'

    pkg {
        userOrg = 'twcable'
        repo = 'aem'
        name = 'Grabbit'

        desc = 'The purpose of this project is to provide a reliable and fast solution for copying content from a Source to Destination. Source and destination can be any AEM instances.'

        websiteUrl = 'https://github.com/TWCable/grabbit'
        issueTrackerUrl = 'https://github.com/TWCable/grabbit/issues'
        vcsUrl = 'https://github.com/TWCable/grabbit.git'
        licenses = ['Apache-2.0']
        labels = ['AEM', 'CQ', 'Content Copy', 'Data Migration']
        attributes = ['plat': ['aem', 'cq']]

        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            //noinspection GrReassignedInClosureLocalVar
            name = project.version.bintrayVersion
            vcsTag = 'v' + project.version
        }
    }
}

bintrayUpload.dependsOn('createPackage')

// **************************************************************************
//
// VERSION CLASS
//
// **************************************************************************

class Version {
    String originalVersion
    String thisVersion
    String status
    Date buildTime


    Version(String versionValue) {
        buildTime = new Date()
        originalVersion = versionValue
        if (originalVersion.endsWith('-SNAPSHOT')) {
            status = 'integration'
            thisVersion = originalVersion - 'SNAPSHOT' + getTimestamp()
        }
        else {
            status = 'release'
            thisVersion = versionValue
        }
    }


    @SuppressWarnings("UnnecessaryQualifiedReference")
    String getTimestamp() {
        // Convert local file timestamp to UTC
        def format = new java.text.SimpleDateFormat('yyyyMMddHHmmss')
        format.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')));
        return format.format(buildTime)
    }


    String toString() {
        originalVersion
    }


    String getBintrayVersion() {
        thisVersion
    }

}
